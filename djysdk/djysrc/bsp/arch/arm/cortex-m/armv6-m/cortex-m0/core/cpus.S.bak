@----------------------------------------------------
@Copyright (C), 2004-2009,  lst.
@版权所有 (C), 2004-2009,   lst.
@所属模块:调度器
@作者：lst
@版本：V1.0.0
@文件描述:调度器中与CPU直接相关的汇编代码，主要是线程切换方面的代码。
@其他说明:
@修订历史:
@2. ...
@1. 日期: 2009-01-04
@   作者: lst
@   新版本号: V1.0.0
@   修改说明: 原始版本
@------------------------------------------------------
.extern      __djy_vm_engine
.extern      int_half_enable_asyn_signal

@注: psp并不保存在栈中，而是在vm->stack中!!

.syntax		unified
.cpu		cortex-m0
.fpu		softvfp
.thumb
.file	"cpus.S"

@----重置线程-----------------------------------------------------------------
@功能:  初始化线程的栈，并挂上线程执行函数，新创建线程时将会调用本函数
@参数:  thread_routine，被重置的线程函数
@       vm，线程虚拟机指针
@返回:  初始化结束后的当前栈指针
@函数原型:void * __asm_reset_thread(void (*thread_routine)(struct event_script *),
@                                        struct  tagThreadVm  *vm)@
@注:根据atpcs，只能使用r0~r3,否则必须压栈
@-----------------------------------------------------------------------------
.text
.align 2
.global __asm_reset_thread
.thumb
.thumb_func
__asm_reset_thread:
    ldr     r2,[r1,#4]          @取虚拟机栈顶指针
    subs    r2,r2,#8
    ldr     r3,=0x01000000      @xpsr的初始值
    str     r3,[r2,#4]
    ldr     r3,=__djy_vm_engine @取虚拟机引擎指针
    str     r3,[r2]
    subs    r2,r2,#14*4        @后退14个寄存器,初始状态r0-r12中，除r0外均无意义，
                                @__vm_engine函数不返回，lr也无意义
    @存在r0中的thread_routine是__vm_engine的参数，切换上下文时，thread_routine将
    @恢复到r0中，根据调用约定，r0的值就是__vm_engine函数的参数。
    str     r0,[r2,#8*4]        @保存 thread_routine指针到r0的位置.
    str     r2,[r1]             @保存vm的当前栈指针到vm->stack中
    bx      lr
@ end of __asm_reset_thread

@----复位老线程，切换到新线程-------------------------------------------------
@功能:  把old_vm的上下文重新初始化到新创建的状态，然后切换到新线程的
@       上下文中继续执行。
@       当一个在常驻内存的虚拟机中处理的事件完成后，不能删除该虚拟机，必须复位
@       该虚拟机，然后切换到其他就绪线程，这时候就需要调用本函数。因此本函数在
@       被复位的线程上下文里面执行。
@参数:  thread_routine，被重置的线程函数
@       new_vm，目标线程的虚拟机
@       old_vm，被复位的虚拟机
@返回:  无
@函数原型:void __asm_reset_switch(void (*thread_routine)(void),
@                           struct  tagThreadVm *new_vm,struct  tagThreadVm *old_vm)@
@注:老线程将被重置，寄存器使用无限制，无须保存
@-----------------------------------------------------------------------------
.text
.align 2
.global __asm_reset_switch
.thumb
.thumb_func
__asm_reset_switch:
    ldr     r7,[r2,#4]          @取老虚拟机栈顶指针
    mov     sp,r7
    ldr     r7,=0x01000000      @xpsr的初始值
    ldr     r6,=__djy_vm_engine    @取虚拟机引擎指针
    push    {r6,r7}             @存pc,xpsr
    sub     sp,sp,#14*4         @后退14个寄存器,初始状态r0-r12中，除r0外均无意义，
                                @__vm_engine函数不返回，lr也无意义
                                @至此，完成老虚拟机复位
    str     r0,[sp,#8*4]        @保存 thread_routine指针.至此，完成老线程重置
    mov     r5,sp
    str     r5,[r2]             @保存当前栈指针到old_vm->stack中

    ldr     r5,[r1]             @取得新上下文指针
    mov     sp,r5
    bl      int_half_enable_asyn_signal @对应done函数开头的 __int_reset_asyn_signal 调用
    svc     0                   @切入新上下文
@ end of __asm_reset_switch

@----启动多事件调度-----------------------------------------------------------
@功能: 系统初始化完成后，启动多事件调度并切入第一个事件.
@参数: new_vm，首次启动的虚拟机控制块指针
@返回: 无
@函数原型: void __asm_start_thread(struct  tagThreadVm  *new_vm)@
@-----------------------------------------------------------------------------
.text
.align 2
.global __asm_start_thread
.thumb
.thumb_func
__asm_start_thread:
    ldr     r1,=msp_top
    msr     msp,r1			    @重置主栈指针，主栈将用于异常和中断
    movs    r1,#2
    msr     control,r1          @切换到特权线程模式，栈指针用psp
    ldr     r1,[r0]             @取得新上下文指针
    mov     sp,r1
    bl      int_half_enable_asyn_signal @线程启动后，调度应该是允许的
    svc     0                   @切入新上下文

@----切入上下文---------------------------------------------------------------
@功能:  不保存原上下文，直接切入新的上下文执行
@参数:  new_vm，待切入的虚拟机控制块指针
@返回:  无
@函数原型: void __asm_turnto_context(struct  tagThreadVm  *new_vm)@
@说明:  当事件完成,就没有必要保存旧事件的上下文,直接切换到新事件即可.
@-----------------------------------------------------------------------------
.text
.align 2
.global __asm_turnto_context
.thumb
.thumb_func
__asm_turnto_context:
    ldr     r1,[r0]             @取得新上下文指针
    mov     sp,r1
    bl      int_half_enable_asyn_signal @对应done函数开头的 int_save_asyn_signal 调用
    svc     0                   @切入新上下文
@ end of __asm_turnto_context

@----上下文切换---------------------------------------------------------------
@功能:  保存当前线程的上下文，切换到新线程的上下文中继续执行。
@参数:  new_sp，新上下文的栈指针
@参数:  old_sp，旧上下文的栈指针的指针，即&vm->stack。无需提供旧上下文栈指针，
@               sp寄存器的当前值就是
@返回:  无
@函数原型: void __asm_switch_context(struct  tagThreadVm *new_vm,struct  tagThreadVm *old_vm)@
@-----------------------------------------------------------------------------
.text
.align 2
.global __asm_switch_context
.thumb
.thumb_func
__asm_switch_context:
    mrs     r3,xpsr
    ldr     r2,=0x01000000
	orrs    r3,r3,r2            @xpsr的T标志读不出来，得手工置位。
    push    {r3}                @保存xpsr
    push    {lr}                @保存PC,从其他线程切换回来时，相当于原线程调用
                                @本函数返回，故用lr替代pc
    mov     r3,r12
    push    {r3,lr}             @保存r12,lr
    push    {r0-r3}             @保存r0-r3
    mov     r2,r10
    mov     r3,r11
    push    {r2,r3}             @根据atpcs，r4~r7入栈前，不能使用，故只能用r2，
                                @r3做临时变量，分两次保存r8~r11
    mov     r2,r8
    mov     r3,r9
    push    {r2,r3}
    push    {r4-r7}
    mov     r2,sp
    str     r2,[r1]             @保存旧上下文栈指针到old_vm->stack

    ldr     r2,[r0]             @取得新上下文指针
    mov     sp,r2
@    bl      int_half_enable_asyn_signal
    svc     0                   @切入新上下文
@ end of __asm_switch_context

@----中断中的上下文切换-------------------------------------------------------
@功能:  保存被中断线程的上下文，切换到新线程的上下文中继续执行。本函数虽然在中
@       断服务函数(非用户用int_isr_connect函数连接的中断服务函数)中，但在ARM
@       中，却运行在svc态
@参数:  new_sp，切换目标虚拟机
@参数:  old_sp，被中断线程虚拟机
@返回:  无
@函数原型: void __asm_switch_context_int(struct tagThreadVm *new_vm,struct tagThreadVm *old_vm)@
@-----------------------------------------------------------------------------
.text
.align 2
.global __asm_switch_context_int
.thumb
.thumb_func
__asm_switch_context_int:
    mrs     r2,psp      @取被中断线程的psp
    str     r2,[r1]     @把psp写入虚拟机数据结构中
    ldr     r2,[r0]     @取待切入线程的psp
    msr     psp,r2      @待切入线程的当前栈指针写入psp，中断返回恢复上下文将以此为准
    bx      lr

.text
.align 2
.global __asm_reset_cpu
.global __asm_reset_cpu_hot
.thumb
.thumb_func
__asm_reset_cpu:
__asm_reset_cpu_hot:
    movs     r0,#0
    mov     pc,r0


.end
